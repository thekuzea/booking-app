plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.openapi.generator' version '4.3.1'
}

group = 'com.thekuzea'
version = '0.0.1'
sourceCompatibility = 10
targetCompatibility = 10

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    implementation 'io.swagger:swagger-annotations:1.6.2'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.1'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/booking-app-server/src/main/resources/api/main.yml"
    outputDir = "$buildDir/generated/openapi"
    apiPackage = "${group}.booking.api"
    invokerPackage = "${group}.booking.api"
    modelPackage = "${group}.booking.api.dto"
    configOptions = [
            dateLibrary  : 'java8',
            interfaceOnly: 'true'
    ]
}

compileJava.dependsOn tasks.openApiGenerate

sourceSets.main {
    java.srcDir "$buildDir/generated/openapi/src/main/java"
    ext.originalJavaSrcDirs = java.srcDirs
}

test {
    useJUnitPlatform()
}
